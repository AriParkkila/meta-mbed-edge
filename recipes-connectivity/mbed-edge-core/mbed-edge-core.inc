DESCRIPTION="mbed-edge-core"

LICENSE="Apache-2.0"
LIC_FILES_CHKSUM = "file://${WORKDIR}/git/LICENSE;md5=1dece7821bf3fd70fe1309eaa37d52a2"

MBED_EDGE_CORE_CONFIG_TRACE_LEVEL ?= "DEBUG"
MBED_EDGE_CORE_CONFIG_FIRMWARE_UPDATE ?= "OFF"
MBED_EDGE_CORE_CONFIG_DEVELOPER_MODE ?= "ON"
MBED_EDGE_CORE_CONFIG_FACTORY_MODE ?= "OFF"
MBED_EDGE_CORE_CONFIG_BYOC_MODE ?= "OFF"

# Patches for quilt goes to files directory
FILESEXTRAPATHS_prepend := "${THISDIR}/files:"

SRCREV = "1d681cd114d91af25947d2505fa7350d35e87565"

RM_WORK_EXCLUDE += "${PN}"

SRC_URI = "git://github.com/ARMmbed/mbed-edge.git \
           file://edge-core.init \
           file://edge-core.service \
           file://edge-core.logrotate \
           file://toolchain.cmake \
           file://0001-disable-Doxygen.patch \
           file://0002-fix-libevent-build-with-CMake-in-Yocto.patch \
           file://0003-fix_libwebsockets_nondebug.manual_patch"
SRC_URI += "\
    ${@bb.utils.contains('MBED_EDGE_CORE_CONFIG_DEVELOPER_MODE','ON','file://mbed_cloud_dev_credentials.c','',d)} \
    ${@bb.utils.contains('MBED_EDGE_CORE_CONFIG_FIRMWARE_UPDATE','ON','file://update_default_resources.c','',d)} \
"

DEPENDS = " libcap mosquitto mercurial-native"
#Required in case PELION_API_KEY is provided
DEPENDS += "python3-native python3-pip-native mercurial-native python3-pyusb-native python3-setuptools-native cmake-native python3-psutil-native"

RDEPENDS_${PN} = " procps bash tar bzip2 rng-tools"

# Installed packages
PACKAGES = "${PN} ${PN}-dbg"
FILES_${PN} += "/wigwag \
                /wigwag/mbed \
                /wigwag/mbed/edge-core \
                /mnt \
                /mnt/config \
                /mnt/cache \
                /mnt/flags"

FILES_${PN}-dbg += "/wigwag/mbed/debug \
                    /usr/src/debug/mbed-edge"

S = "${WORKDIR}/git"

inherit cmake update-rc.d systemd

EXTRA_OECMAKE += " \
    -DCMAKE_BUILD_TYPE=Debug \
    -DTARGET_TOOLCHAIN=yocto \
    -DMBED_CONF_MBED_TRACE_ENABLE=1 \
    -DTARGET_CONFIG_ROOT=${WORKDIR} \
    -DTRACE_LEVEL=${MBED_EDGE_CORE_CONFIG_TRACE_LEVEL} \
    -DFIRMWARE_UPDATE=${MBED_EDGE_CORE_CONFIG_FIRMWARE_UPDATE} \
    -DDEVELOPER_MODE=${MBED_EDGE_CORE_CONFIG_DEVELOPER_MODE} \
    -DFACTORY_MODE=${MBED_EDGE_CORE_CONFIG_FACTORY_MODE} \
    -DBYOC_MODE=${MBED_EDGE_CORE_CONFIG_BYOC_MODE} \
    ${MBED_EDGE_CUSTOM_CMAKE_ARGUMENTS} \
    "

INITSCRIPT_NAME = "mbed-edge-core"
INITSCRIPT_PARAMS = "defaults 85 15"

SYSTEMD_PACKAGES = "${PN}"
SYSTEMD_SERVICE_${PN} = "edge-core.service"

do_configure_prepend() {
    cd ${S}
    git submodule update --init --recursive

    if [ ! -z "${PELION_API_KEY}" ]; then
        #Generate a developer cert
        cd ${S}/lib/mbed-cloud-client
        export PYTHONPATH=$PYTHONPATH:`pwd`/recipe-sysroot-native/usr/lib/python3.8
        export PATH=$PYTHONPATH:$PATH
        pip3 install mbed-cli click==7.0 requests
        pip3 install pyyaml jsonschema mbed_cloud_sdk jinja2 mbed_ls mbed_host_tests mbed_greentea pyelftools manifest_tool==1.5.2 icetea pycryptodome cmsis_pack_manager cryptography cbor
        mbed new .
        mbed config -G CLOUD_SDK_API_KEY "${PELION_API_KEY}"
        mbed dm init -d "pelion.com" --model-name "mbed-edge-device" -q
        cp mbed_cloud_dev_credentials.c "${WORKDIR}/mbed_cloud_dev_credentials.c"
        cd ${S}
    fi
    [ -f "${WORKDIR}/mbed_cloud_dev_credentials.c" ] && {
        mv "${WORKDIR}/mbed_cloud_dev_credentials.c" config/
    }
    [ -f "${WORKDIR}/update_default_resources.c" ] && {
        mv "${WORKDIR}/update_default_resources.c" config/
    }

    # Temp fix for libwebsockets non-debug build
    patch -p1 < ${WORKDIR}/0003-fix_libwebsockets_nondebug.manual_patch
}

do_install() {
    install -d "${D}/etc/init.d/"
    install -m 755 "${WORKDIR}/edge-core.init" "${D}/etc/init.d/${INITSCRIPT_NAME}"

    install -d "${D}${systemd_system_unitdir}"
    install -m 755 "${WORKDIR}/edge-core.service" "${D}${systemd_system_unitdir}/edge-core.service"

    install -d "${D}/wigwag/mbed"
    install "${WORKDIR}/build/bin/edge-core" "${D}/wigwag/mbed"

    install -d "${D}${sysconfdir}/logrotate.d/"
    install -m 644 "${WORKDIR}/edge-core.logrotate" "${D}${sysconfdir}/logrotate.d"

    install -d "${D}/mnt/config"
    install -d "${D}/mnt/flags"
    install -d "${D}/mnt/cache"

}

